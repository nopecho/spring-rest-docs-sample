plugins {
    id "com.epages.restdocs-api-spec" version "${restdocsApiSpecVersion}"
    id "org.hidetake.swagger.generator" version "2.19.2"
}

dependencies {
    implementation(project(":core:adapter:adapter-web"))
    implementation(project(":core:infrastructure"))

    testImplementation project(":core:domain")
    testImplementation project(":core:application")
    testImplementation "org.testcontainers:testcontainers:${testContainerVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testContainerVersion}"

    testImplementation "org.springframework.restdocs:spring-restdocs-restassured"
    testImplementation "io.rest-assured:kotlin-extensions:${restassuredVersion}"
    testImplementation "io.rest-assured:json-schema-validator:${restassuredVersion}"

    testImplementation "com.epages:restdocs-api-spec:${restdocsApiSpecVersion}"
    testImplementation "com.epages:restdocs-api-spec-restassured:${restdocsApiSpecVersion}"
    swaggerUI 'org.webjars:swagger-ui:5.10.3'
}

tasks.register("startDocker", Exec) {
    workingDir rootProject.rootDir
    commandLine "sh", "-c", "make up"
}

tasks.register("initDocker", Exec) {
    dependsOn "startDocker"
    workingDir rootProject.rootDir
    commandLine "sh", "-c", "make health"
}

test {
    dependsOn "initDocker"
    finalizedBy "openapi3"
}

openapi3 {
    servers = [
            { url = "http://localhost:8888" },
            { url = "https://dev-sample.api" },
            { url = "https://sample.api" }
    ]
    title = 'Sample API'
    description = 'My Sample API description'
    version = '0.1.0'
    format = 'yaml'
    copy {
        from "${project.buildDir}/api-spec"
        into "${rootDir}"
    }
}

swaggerSources {
    sample {
        inputFile = file("${project.buildDir}/api-spec/openapi3.yaml")
    }
}

generateReDocSample {
    dependsOn "openapi3"
    doLast {
        copy {
            from "${project.buildDir}/redoc-sample"
            into "${project.buildDir}/resources/main/static/docs"
        }
    }
}

generateSwaggerUISample {
    dependsOn "openapi3"
    doLast {
        copy {
            from "${project.buildDir}/swagger-ui-sample"
            into "${project.buildDir}/resources/main/static/docs"
        }
    }
}

bootJar {
    dependsOn generateSwaggerUISample
}