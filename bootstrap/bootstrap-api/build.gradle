plugins {
    id "com.epages.restdocs-api-spec" version "${restdocsApiSpecVersion}"
    id "org.hidetake.swagger.generator" version "2.19.2"
}

dependencies {
    implementation(project(":core:adapter:adapter-web"))
    implementation(project(":core:infrastructure"))

    swaggerUI 'org.webjars:swagger-ui:5.10.3'
    testImplementation "org.springframework.restdocs:spring-restdocs-restassured"
    testImplementation "io.rest-assured:kotlin-extensions:${restassuredVersion}"
    testImplementation "io.rest-assured:json-schema-validator:${restassuredVersion}"
    testImplementation "com.epages:restdocs-api-spec:${restdocsApiSpecVersion}"
    testImplementation "com.epages:restdocs-api-spec-restassured:${restdocsApiSpecVersion}"
}

openapi3 {
    server = 'http://localhost:8888'
    title = 'Sample API'
    description = 'My Sample API description'
    version = '0.1.0'
    format = 'yaml'
}

swaggerSources {
    sample {
        inputFile = file("${project.buildDir}/api-spec/openapi3.yaml")
    }
}

generateSwaggerUISample {
    dependsOn "openapi3"
}

tasks.register("copySwaggerUI") {
    dependsOn "generateSwaggerUISample"
    it.doFirst {
        delete "${project.buildDir}/resources/main/static/docs"
    }
    it.doLast {
        copy {
            from "${generateSwaggerUISample.outputDir}"
            into "${project.buildDir}/resources/main/static/docs"
        }
        delete "${generateSwaggerUISample.outputDir}"
    }
}

bootJar {
    dependsOn "copySwaggerUI"
}